plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    // Lombok plugin
    // id "io.freefair.lombok" version "5.1.1"
}

application {
    // Define the main class for the application.
    mainClassName = 'sisop.oliveiracley.VM'
}

run {
    systemProperties = System.properties
}

repositories {
    // Manage packages from Maven Central repository
    // https://repo.maven.apache.org/maven2/ 
    mavenCentral()
}


dependencies {
    // <LANGUAGE> Groovy
    implementation 'org.codehaus.groovy:groovy:3.0.5'
    // implementation 'org.codehaus.groovy:groovy:3.0.0-rc-2'

    // Beans
    implementation 'javax.enterprise:cdi-api:2.0.SP1'
    // compile 'groovyx.gdi:gdi:0.1a-SNAPSHOT'

	// <TESTS> JUnit 5 
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}


// Tests configuration
test {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest summary {                           \n" +
                    "\t${result.testCount} tests found          \n" +
                    "\t-------------                            \n" +
                    "\t${result.successfulTestCount} succeeded, \n" +
                    "\t${result.failedTestCount} failed,        \n" +
                    "\t${result.skippedTestCount} skipped       \n" +
                    "}"
        }
    }

    filter {
        // Include all classes with Test suffix
        includeTestsMatching "*Test"

        // Include all tests from package
        includeTestsMatching "sisop.oliveiracley.*"
    }
}

//  TASKS ------------------

task artifact(type: Zip){
    
    include '*'
    include '*/**'
    exclude 'RELAESE_Artifact.zip'
    exclude '/.gradle'
    exclude 'gradle'
    exclude '/build/**'
    into "SisOp_VM"

    archiveFileName = 'RELAESE_Artifact.zip'
    destinationDirectory = file('./')

    from './'
}